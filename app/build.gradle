plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'org.jetbrains.kotlin.plugin.parcelize'
}

android {
    namespace APPLICATION_ID

    defaultConfig {
        applicationId APPLICATION_ID
        versionCode gitVersionCode.toInteger()
        versionName gitVersionName

        archivesBaseName = "${rootProject.name}-v${versionName}-${versionCode}"
    }

    def releaseSigning
    try {
        releaseSigning = signingConfigs.create("release") {
            storeFile file(STORE_FILE_PATH)
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storePassword STORE_PASSWORD
        }
    } catch (Exception ignored) {
        releaseSigning = signingConfigs.debug
    }

    buildTypes {
        debug {
            signingConfig releaseSigning
        }
        release {
            signingConfig releaseSigning
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

dependencies {
    api projects.common
    api projects.xposed

    //remove this line will cause proguard to remove code from library module
    compileOnly 'de.robv.android.xposed:api:82'

    //compose
    implementation platform('androidx.compose:compose-bom:2025.06.00')
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.animation:animation"
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"

    implementation "androidx.activity:activity-compose:1.10.1"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.1'
    implementation "androidx.navigation:navigation-compose:2.9.0"

    implementation "com.google.accompanist:accompanist-drawablepainter:0.37.3"

    implementation 'de.charlex.compose:html-text:1.6.0'
}
