import com.android.build.gradle.api.AndroidBasePlugin

plugins {
    id 'com.android.application' version '8.5.2' apply false
    id 'com.android.library' version '8.5.2' apply false
    id 'org.jetbrains.kotlin.android' version '2.0.10' apply false
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.10' apply false
    id 'org.jetbrains.kotlin.plugin.parcelize' version '2.0.10' apply false
}

allprojects {
    plugins.withType(AndroidBasePlugin).configureEach {
        project.android {
            compileSdk 35
            defaultConfig {
                minSdk 26
                targetSdk compileSdk
                vectorDrawables.useSupportLibrary = true
            }
        }
    }

    plugins.withType(JavaBasePlugin).configureEach {
        java.toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}

final def getVersionCode = providers.exec {
    commandLine('git', 'rev-list', '--first-parent', '--count', 'HEAD')
}.standardOutput.asText.get().trim()

final def getVersionName = providers.exec {
    commandLine('git', 'describe', '--tags', '--dirty')
}.standardOutput.asText.get().trim() - "v"

ext {
    gitVersionCode = getVersionCode
    gitVersionName = getVersionName
    APPLICATION_ID = "one.yufz.hmspush"
}

tasks.register('clean') {
    delete rootProject.layout.buildDirectory
}
